BACKUP ~AC_QUEST/backup~
//AUTHOR ~whiteagnus@baldursgatemods.de~ 
SUPPORT ~https://baldurs-gate.de/index.php?threads/ascalons-questpack.39286/~

VERSION ~5.2~  

AUTO_TRA "AC_QUEST/TRA/autotra/%s"//%"

ALWAYS

  INCLUDE ~AC_QUEST/lib/g3_tutu_cpmvars.tpa~
  INCLUDE ~AC_QUEST/lib/g3_bgt_cpmvars.tpa~
  INCLUDE ~AC_QUEST/lib/liam_bgee_vars.tpa~

/* Declaration of EET Platform Variables */
  ACTION_IF GAME_IS "eet" BEGIN
    INCLUDE ~EET/other/cpmvars/eet_cpmvars.tpa~
  END

  INCLUDE ~AC_QUEST/lib/ac_quest_cpmvars.tpa~


/* DavidW's code for unifying the area script patching. With many thanks! 
To be used later in the form:

  LAF extend_area_script STR_VAR area="%Candlekeep%" top="bg1re/script/AR2600.baf"   END
*/
  DEFINE_ACTION_FUNCTION extend_area_script 
    STR_VAR area=""
            top=""
            bottom=""
    BEGIN
      // find the area script from the area file
      COPY_EXISTING "%area%.are" override
           READ_ASCII 0x94 script
      BUT_ONLY
      // if it doesn't exist, give it a default name
      ACTION_IF "%script%" STRING_EQUAL "" BEGIN
          OUTER_INNER_PATCH_SAVE script "%area%" BEGIN
                REPLACE_TEXTUALLY "FW" "_AR" // TUTU naming conventions
          END

  // added by argent77: assign area script if needed
          COPY_EXISTING "%area%.are" override
                WRITE_ASCIIE 0x94 ~%script%~ #8


      END
      // extend the script
      ACTION_IF "%top%" STRING_COMPARE "" BEGIN
        EXTEND_TOP "%script%.bcs" "%top%" EVALUATE_BUFFER
      END
      ACTION_IF "%bottom%" STRING_COMPARE "" BEGIN
       EXTEND_BOTTOM "%script%.bcs" "%bottom%" EVALUATE_BUFFER
      END
    END 


/* Handling of correctly formatted tra-files!! ---------------------- */

/* all following actions are only processed ONCE for the whole mod, independent of un- and reinstalling of single components */

  ACTION_IF !FILE_EXISTS ~AC_QUEST/Install/AC_QUESTreinstall.mrk~ BEGIN

/*
* Copies tra files into the autotra-folder (to leave the originals untouched)
*/
    DEFINE_ACTION_FUNCTION autotra_workaround BEGIN
      COPY ~AC_QUEST/tra/german~ ~AC_QUEST/tra/autotra/%LANGUAGE%~
      COPY ~AC_QUEST/tra/%LANGUAGE%~ ~AC_QUEST/tra/autotra/%LANGUAGE%~
    END

    LAF autotra_workaround END

    ACTION_DEFINE_ARRAY tra#reload BEGIN setup-AC_QUEST END
    ACTION_DEFINE_ARRAY tra#noconvert BEGIN setup-install END

    LAF HANDLE_CHARSETS
      INT_VAR
			infer_charsets = 1
      STR_VAR
			tra_path = EVAL ~AC_QUEST/tra/autotra~
			noconvert_array = tra#noconvert
			reload_array = tra#reload
			iconv_path = ~AC_QUEST/tools/iconv~
    END



//CamDawgs CD_STATE_NOTVALID zum Checken der Dialogfähigkeit eines Char
//Thank you very much, CamDawg!
    APPEND ~STATE.IDS~ // adds custom IsValidForPartyDialogue state
      ~0x80101FEF CD_STATE_NOTVALID~
      UNLESS ~CD_STATE_NOTVALID~

    APPEND ~shoutids.ids~ ~99 HELPME~ UNLESS ~^99~

//Fix for ToB Shoutaction (appears on Tutu)
    COPY_EXISTING ~action.ids~ ~override~
      REPLACE_TEXTUALLY ~^106 Shout(.*$~ ~106 Shout(I:ID*SHOUTIDS)~
      BUT_ONLY_IF_IT_CHANGES

    APPEND ~STATE.IDS~ ~0x00000FC0 STATE_REALLY_DEAD~ 
      UNLESS ~STATE_REALLY_DEAD~

    ACTION_IF GAME_IS ~bgt~ THEN BEGIN
      EXTEND_BOTTOM ~AR0602.bcs~ ~AC_QUEST/BAF/AC0602.BAF~
    END

    ACTION_IF GAME_IS ~bgee~ THEN BEGIN
      COPY ~AC_QUEST/TRA/english/sounds~ ~override~
      ACTION_IF DIRECTORY_EXISTS ~AC_QUEST/tra/%LANGUAGE%/sounds~ THEN BEGIN
        COPY ~AC_QUEST/TRA/%LANGUAGE%/sounds~ ~override~
      END
    END

    ACTION_IF GAME_IS ~bgt bgee tutu_totsc eet~ THEN BEGIN

      ACTION_IF NOT FILE_EXISTS_IN_GAME ~observe.cre~ THEN BEGIN
        COPY ~AC_QUEST/cre/observe.cre~ ~override~
      END

      ACTION_IF NOT FILE_EXISTS_IN_GAME ~B1-12M4.ITM~ THEN BEGIN
        COPY ~AC_QUEST/itm/B1-12M4.ITM~ ~override~
        SAY NAME2 #7019
      END

      ACTION_IF NOT FILE_EXISTS_IN_GAME ~CHAN09.ITM~ THEN BEGIN
        COPY ~AC_QUEST/itm/CHAN09.ITM~ ~override~
        SAY NAME2 #21308
        SAY DESC @80049
      END

      ACTION_IF NOT FILE_EXISTS_IN_GAME ~CLCK29.ITM~ THEN BEGIN
        COPY ~AC_QUEST/itm/CLCK29.ITM~ ~override~
        SAY NAME2 @80050
        SAY DESC @80051
      END

      ACTION_IF NOT FILE_EXISTS_IN_GAME ~HAMM07.ITM~ THEN BEGIN
        COPY ~AC_QUEST/itm/HAMM07.ITM~ ~override~
        SAY NAME2 @80052
        SAY DESC @80053
      END

      ACTION_IF NOT FILE_EXISTS_IN_GAME ~KEY27.ITM~ THEN BEGIN
        COPY ~AC_QUEST/itm/KEY27.ITM~ ~override~
        SAY NAME1 @80054
        SAY NAME2 @80054
        SAY UNIDENTIFIED_DESC @80055
        SAY DESC @80055
      END

      ACTION_IF NOT FILE_EXISTS_IN_GAME ~LEAT14.ITM~ THEN BEGIN
        COPY ~AC_QUEST/itm/LEAT14.ITM~ ~override~
        SAY NAME2 @80056
        SAY DESC @80057
      END

      ACTION_IF NOT FILE_EXISTS_IN_GAME ~PLAT21.ITM~ THEN BEGIN
        COPY ~AC_QUEST/itm/PLAT21.ITM~ ~override~
        SAY NAME2 @80058
        SAY DESC @80059
      END

      ACTION_IF NOT FILE_EXISTS_IN_GAME ~SPECTR.ITM~ THEN BEGIN
        COPY ~AC_QUEST/itm/SPECTR.ITM~ ~override~
      END

      ACTION_IF NOT FILE_EXISTS_IN_GAME ~DEMBAL01.BCS~ THEN BEGIN
        COMPILE ~AC_QUEST/baf/DEMBAL01.BAF~
      END

      ACTION_IF NOT FILE_EXISTS_IN_GAME ~PRIES6C.BCS~ THEN BEGIN
        COMPILE ~AC_QUEST/baf/PRIES6C.BAF~
      END

      ACTION_IF NOT FILE_EXISTS_IN_GAME ~PRIES10B.BCS~ THEN BEGIN
        COMPILE ~AC_QUEST/baf/PRIES10B.BAF~
      END

      ACTION_IF NOT FILE_EXISTS_IN_GAME ~INPAIVSG.BCS~ THEN BEGIN
        COMPILE ~AC_QUEST/baf/INPAIVSG.BAF~
      END
	

      COMPILE ~AC_QUEST/baf/ACUITM.baf~

    END

    COPY ~AC_QUEST/install/component.xx~             ~AC_QUEST/Install/AC_QUESTreinstall.mrk~

  END //AC_QUESTreinstall.mrk - do only once


    //TRAs declared in LANGUAGE must be reloaded
		
  LOAD_TRA ~AC_QUEST/tra/autotra/%LANGUAGE%/setup-AC_QUEST.tra~
  ACTION_IF GAME_IS ~bgee eet~ THEN BEGIN
    LOAD_TRA ~AC_QUEST/tra/autotra/%LANGUAGE%/setup-AC_QUEST_ee.tra~
  END

END //ALWAYS




LANGUAGE ~Deutsch (German)~ 
	 ~german~ 
	 ~AC_QUEST/TRA/german/setup-AC_QUEST.tra~
	 ~AC_QUEST/TRA/german/setup-install.tra~

LANGUAGE ~English~ 
	 ~english~ 
	 ~AC_QUEST/TRA/english/setup-AC_QUEST.tra~
	 ~AC_QUEST/TRA/english/setup-install.tra~

LANGUAGE ~Russian~ 
	 ~russian~ 
	 ~AC_QUEST/TRA/russian/setup-AC_QUEST.tra~
	 ~AC_QUEST/TRA/english/setup-install.tra~

LANGUAGE ~Italian~ 
	 ~italian~ 
	 ~AC_QUEST/TRA/italian/setup-AC_QUEST.tra~
	 ~AC_QUEST/TRA/italian/setup-install.tra~
	 
LANGUAGE ~French~ 
	 ~french~ 
	 ~AC_QUEST/TRA/french/setup-AC_QUEST.tra~
	 ~AC_QUEST/TRA/french/setup-install.tra~
	 
LANGUAGE ~Spanish~ 
	 ~spanish~ 
	 ~AC_QUEST/TRA/spanish/setup-AC_QUEST.tra~
	 ~AC_QUEST/TRA/spanish/setup-install.tra~

LANGUAGE ~Simplified Chinese~ 
	 ~schinese~ 
	 ~AC_QUEST/TRA/schinese/setup-AC_QUEST.tra~
	 ~AC_QUEST/TRA/schinese/setup-install.tra~

////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////
////////////																			////////////
////////////									BEGIN PART 1							////////////
////////////																			////////////
////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////

       
//BEGIN @1
//REQUIRE_PREDICATE GAME_IS ~bgt bgee tob bg2ee tutu_totsc eet~ @2



////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////
////////////																			////////////
////////////						1.	Der Lohn für die Mühe							////////////
////////////																			////////////
////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////

BEGIN @5000003 /* Der Lohn für die Mühe */
LABEL AscalonsQuestpack-JobWellPaid
REQUIRE_PREDICATE GAME_IS ~bgt bgee tutu_totsc eet~ @5000004
FORBID_COMPONENT ~EET_end.tp2~ ~0~ @5000016
FORBID_COMPONENT ~EET_end.tp2~ ~1~ @5000016


ADD_JOURNAL TITLE (@90001) @10001 @10002 @10003 USING ~AC_QUEST/tra/autotra/%LANGUAGE%/setup-AC_QUEST.tra~

COPY ~AC_QUEST/cre/ACQ11001.CRE~ ~override~
SAY NAME1 @6
SAY NAME2 @7

COPY ~AC_QUEST/cre/ACQ11002.CRE~ ~override~
SAY NAME1 @8
SAY NAME2 @9

COPY ~AC_QUEST/cre/ACQ11003.CRE~ ~override~
SAY NAME1 @10
SAY NAME2 @11

COPY_EXISTING ~%Beregost%.are~ ~OVERRIDE~
LPF fj_are_structure
    INT_VAR
    fj_loc_x          = 2608
    fj_loc_y          = 830
    fj_dest_x         = 2608
    fj_dest_y         = 830
    fj_orientation    = 12
    STR_VAR
    fj_structure_type = actor
    fj_name           = Agnus
    fj_cre_resref     = ACQ11001
  END

LPF fj_are_structure
    INT_VAR
    fj_loc_x          = 2657
    fj_loc_y          = 812
    fj_dest_x         = 2657
    fj_dest_y         = 812
    fj_orientation    = 0
    STR_VAR
    fj_structure_type = actor
    fj_name           = Theliel
    fj_cre_resref     = ACQ11002
  END
  
LPF fj_are_structure
    INT_VAR
    fj_loc_x          = 2682
    fj_loc_y          = 865
    fj_dest_x         = 2682
    fj_dest_y         = 865
    fj_orientation    = 7
    STR_VAR
    fj_structure_type = actor
    fj_name           = Udolf
    fj_cre_resref     = ACQ11003
  END
  
COMPILE ~AC_QUEST/D/ACQ11000.D~
EVAL
COMPILE ~AC_QUEST/BAF/ACQ11002.BAF~

////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////
////////////																			////////////
////////////							2.	EIN FRESSEN FÜR DIE GNOLLE					////////////
////////////																			////////////
////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////

BEGIN @5000005
LABEL AscalonsQuestpack-FeastforGnolls
REQUIRE_PREDICATE GAME_IS ~bgt bgee tutu_totsc eet~ @5000004
FORBID_COMPONENT ~EET_end.tp2~ ~0~ @5000016
FORBID_COMPONENT ~EET_end.tp2~ ~1~ @5000016

ADD_JOURNAL TITLE (@90011) @10008 @10009 @10024 @10025 @10026 @10027 USING ~AC_QUEST/tra/autotra/%LANGUAGE%/setup-AC_QUEST.tra~



COPY ~AC_QUEST/cre/ACQ12001.CRE~ ~override~
SAY NAME1 @12
SAY NAME2 @12
SAY INITIAL_MEETING @80001
SAY DAMAGE @80002
SAY DYING @80003
SAY SELECT_COMMON1 @80004

COPY ~AC_QUEST/cre/ACQ12002.CRE~ ~override~
SAY NAME1 @13
SAY NAME2 @14
//SAY INITIAL_MEETING @80005
SAY DAMAGE @80006
SAY DYING @80007
SAY SELECT_COMMON1 @80008

COPY ~AC_QUEST/cre/ACQ12003.CRE~ ~override~
SAY NAME1 @15
SAY NAME2 @15
SAY INITIAL_MEETING @80009
SAY SELECT_COMMON1 @80009

COPY ~AC_QUEST/cre/ACQ12004.CRE~ ~override~
SAY NAME1 @16
SAY NAME2 @17
SAY INITIAL_MEETING @80010
SAY DAMAGE @80011
SAY DYING @80012
SAY SELECT_COMMON1 @80013
SAY SELECT_COMMON2 @80014
SAY SELECT_COMMON3 @80015

COPY ~AC_QUEST/cre/ACGNOLL1.CRE~ ~override~
SAY NAME1 @18
SAY NAME2 @18
/*PATCH_IF GAME_IS ~bgt~ THEN BEGIN
ADD_CRE_ITEM ~RNDTRE08~ #1 #0 #0 ~NONE~ ~INV~
ADD_CRE_ITEM ~RNDTRE08~ #1 #0 #0 ~NONE~ ~INV~
ADD_CRE_ITEM ~RNDTRE08~ #1 #0 #0 ~NONE~ ~INV~
END
*/
//PATCH_IF GAME_IS ~bgee eet tutu_totsc~ THEN BEGIN
ADD_CRE_ITEM ~RNDTRE04~ #1 #0 #0 ~NONE~ ~INV~
ADD_CRE_ITEM ~RNDTRE04~ #1 #0 #0 ~NONE~ ~INV~
ADD_CRE_ITEM ~RNDTRE04~ #1 #0 #0 ~NONE~ ~INV~
//END

COPY ~AC_QUEST/cre/ACGNOLL2.CRE~ ~override~
SAY NAME1 @19
SAY NAME2 @19
/*PATCH_IF GAME_IS ~bgt~ THEN BEGIN
ADD_CRE_ITEM ~RNDTRE08~ #1 #0 #0 ~NONE~ ~INV~
ADD_CRE_ITEM ~RNDTRE08~ #1 #0 #0 ~NONE~ ~INV~
ADD_CRE_ITEM ~RNDTRE08~ #1 #0 #0 ~NONE~ ~INV~
END
*/
//PATCH_IF GAME_IS ~bgee eet tutu_totsc~ THEN BEGIN
ADD_CRE_ITEM ~RNDTRE04~ #1 #0 #0 ~NONE~ ~INV~
ADD_CRE_ITEM ~RNDTRE04~ #1 #0 #0 ~NONE~ ~INV~
ADD_CRE_ITEM ~RNDTRE04~ #1 #0 #0 ~NONE~ ~INV~
//END

COPY_EXISTING ~%Nashkel_TempleofHelm%.are~ ~OVERRIDE~
LPF fj_are_structure
    INT_VAR
    fj_loc_x          = 324
    fj_loc_y          = 312
    fj_dest_x         = 324
    fj_dest_y         = 312
    fj_orientation    = 0
    STR_VAR
    fj_structure_type = actor
    fj_name           = Barin
    fj_cre_resref     = ACQ12004
  END
  
COPY_EXISTING ~%DryadFalls%.are~ ~OVERRIDE~
LPF fj_are_structure
    INT_VAR
    fj_loc_x          = 1749
    fj_loc_y          = 3386
    fj_dest_x         = 1749
    fj_dest_y         = 3386
    fj_orientation    = 15
    fj_schedule		  = 0xfff0003f
    STR_VAR
    fj_structure_type = actor
    fj_name           = Marina
    fj_cre_resref     = ACQ12003
  END
  
COPY_EXISTING ~%Nashkel%.are~ ~OVERRIDE~
LPF fj_are_structure
    INT_VAR
    fj_loc_x          = 2609
    fj_loc_y          = 2340
    fj_dest_x         = 2609
    fj_dest_y         = 2340
    fj_orientation    = 2
    STR_VAR
    fj_structure_type = actor
    fj_name           = Frau B
    fj_cre_resref     = ACQ12001
  END

COMPILE ~AC_QUEST/D/ACQ12000.D~
EVAL
COMPILE ~AC_QUEST/BAF/ACQ12001.BAF~
COMPILE ~AC_QUEST/BAF/ACQ12002.BAF~
EVAL
COMPILE ~AC_QUEST/BAF/ACQ12004.BAF~
COMPILE ~AC_QUEST/BAF/ACQ12CUT.BAF~
EVAL
COMPILE ~AC_QUEST/BAF/ACQ12CU2.BAF~
EVAL

/*
EXTEND_TOP ~%GnollStronghold_BCS%.bcs~ ~AC_QUEST/baf/AC4100.baf~
EVAL
*/
LAF extend_area_script STR_VAR area="%GnollStronghold%" top="AC_QUEST/baf/AC4100.baf"   END


COPY ~AC_QUEST/ITM/ACQ12000.ITM~ ~override~
SAY NAME1 #6348
SAY NAME2 @20
SAY UNIDENTIFIED_DESC #17054
SAY DESC @21

////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////
////////////																			////////////
////////////								3.	Hopsi									////////////
////////////																			////////////
////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////


BEGIN @5000019 /* ~Original version: Ugh and Hopsi are inside the cave.~ */
LABEL AscalonsQuestpack-Rabbit
SUBCOMPONENT @5000006 /* ~Hopsi~ */
REQUIRE_PREDICATE GAME_IS ~bgt bgee tutu_totsc eet~ @5000004
FORBID_COMPONENT ~EET_end.tp2~ ~0~ @5000016
FORBID_COMPONENT ~EET_end.tp2~ ~1~ @5000016

INCLUDE ~AC_QUEST/tpa/hopsi.tpa~

COPY_EXISTING ~%SouthBeregostRoad_Cave%.are~ ~OVERRIDE~
LPF fj_are_structure
	INT_VAR
	fj_loc_x          = 484
	fj_loc_y          = 294
	fj_dest_x         = 484
	fj_dest_y         = 294
	fj_orientation    = 4
	STR_VAR
	fj_structure_type = actor
	fj_name           = Ugh
	fj_cre_resref     = ACQ13002
END
LPF fj_are_structure
	INT_VAR
	fj_loc_x          = 444
	fj_loc_y          = 293
	fj_dest_x         = 444
	fj_dest_y         = 293
	fj_orientation    = 11
	STR_VAR
	fj_structure_type = actor
	fj_name           = Hopsi
	fj_cre_resref     = ACQ13003
END


BEGIN @5000020 /* ~Alternative for compatibility: Ugh and Hopsi are outside.~ */
LABEL AscalonsQuestpack-Rabbit_alternative
SUBCOMPONENT @5000006 /* ~Hopsi~ */
REQUIRE_PREDICATE GAME_IS ~bgt bgee tutu_totsc eet~ @5000004
FORBID_COMPONENT ~EET_end.tp2~ ~0~ @5000016
FORBID_COMPONENT ~EET_end.tp2~ ~1~ @5000016

INCLUDE ~AC_QUEST/tpa/hopsi.tpa~

COPY_EXISTING ~%SouthBeregostRoad%.are~ ~OVERRIDE~
LPF fj_are_structure
	INT_VAR
	fj_loc_x          = 1055
	fj_loc_y          = 2232
	fj_dest_x         = 1055
	fj_dest_y         = 2232
	fj_orientation    = 13
	STR_VAR
	fj_structure_type = actor
	fj_name           = Ugh
	fj_cre_resref     = ACQ13002
END
LPF fj_are_structure
	INT_VAR
	fj_loc_x          = 1128
	fj_loc_y          = 2296
	fj_dest_x         = 1128
	fj_dest_y         = 2296
	fj_orientation    = 4
	STR_VAR
	fj_structure_type = actor
	fj_name           = Hopsi
	fj_cre_resref     = ACQ13003
END



////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////
////////////																			////////////
////////////							4.	Schmuggelnde Halblinge						////////////
////////////																			////////////
////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////
////////////																			////////////
////////////							5.	Der verlorene Sohn							////////////
////////////																			////////////
////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////

BEGIN @5000007
LABEL AscalonsQuestpack-LostSon
REQUIRE_PREDICATE GAME_IS ~bgt bgee tutu_totsc eet~ @5000004
FORBID_COMPONENT ~EET_end.tp2~ ~0~ @5000016
FORBID_COMPONENT ~EET_end.tp2~ ~1~ @5000016

ADD_JOURNAL TITLE (@90006) @10010 @10028 @10029 @10030 @10031 @10032 USING ~AC_QUEST/tra/autotra/%LANGUAGE%/setup-AC_QUEST.tra~

COMPILE ~AC_QUEST/D/ACQ15000.D~
EVAL
COMPILE ~AC_QUEST/BAF/ACQ15001.BAF~
COMPILE ~AC_QUEST/BAF/ACQ15002.BAF~
EVAL
/*
EXTEND_BOTTOM ~%CloakwoodMines_BCS%.bcs~ ~AC_QUEST/BAF/AC8600.BAF~
*/
LAF extend_area_script STR_VAR area="%CloakwoodMines%" bottom="AC_QUEST/BAF/AC8600.BAF"   END

EXTEND_TOP ~BALDUR.bcs~ ~AC_QUEST/BAF/ACBALDUR.BAF~

COPY_EXISTING ~MISC54.ITM~ ~Override/ACQ15002.ITM~
SAY NAME1 @29
SAY NAME2 @29
SAY UNIDENTIFIED_DESC @30
SAY DESC @30

COPY_EXISTING ~BOW07.ITM~ ~Override/ACQ15001.ITM~
SAY NAME1 #6860
SAY NAME2 @31
SAY UNIDENTIFIED_DESC #6862
PATCH_IF GAME_IS ~tutu_totsc bgt~ BEGIN
SAY DESC @32
END
PATCH_IF GAME_IS ~bgee eet~ BEGIN
SAY DESC @100032
END

COPY ~AC_QUEST/cre/ACQ15001.CRE~ ~override~
SAY NAME1 @33
SAY NAME2 @33

COPY ~AC_QUEST/cre/ACQ15002.cre~ ~override~
SAY NAME1 @29
SAY NAME2 @29
SAY INITIAL_MEETING @80023
SAY DAMAGE @80024
SAY DYING @80025
SAY SELECT_COMMON1 @80026
SAY SELECT_COMMON2 @80027

COPY_EXISTING ~%CloakwoodMines_L2%.are~ ~OVERRIDE~
LPF fj_are_structure
	INT_VAR
	fj_loc_x          = 628
	fj_loc_y          = 400
	fj_dest_x         = 628
	fj_dest_y         = 400
	fj_orientation    = 10
	STR_VAR
	fj_structure_type = actor
	fj_name           = Perwell
	fj_cre_resref     = ACQ15002
END

COPY_EXISTING ~%CloakwoodLodge%.are~ ~OVERRIDE~
LPF fj_are_structure
	INT_VAR
	fj_loc_x          = 4810
	fj_loc_y          = 2067
	fj_dest_x         = 4810
	fj_dest_y         = 2067
	fj_orientation    = 14
	STR_VAR
	fj_structure_type = actor
	fj_name           = Aphriel
	fj_cre_resref     = ACQ15001
END

////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////
////////////																			////////////
////////////							6.	Von Wölfen und Männern						////////////
////////////																			////////////
////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////

BEGIN @5000008 /* ~Quest uses hut near Thalantyr in High Hedge.~ */
LABEL AscalonsQuestpack-WolvesMen_Highhedge
SUBCOMPONENT @5000009 /* ~Von Wölfen und Männern~ */
REQUIRE_PREDICATE GAME_IS ~bgt bgee tutu_totsc eet~ @5000004
FORBID_COMPONENT ~EET_end.tp2~ ~0~ @5000016
FORBID_COMPONENT ~EET_end.tp2~ ~1~ @5000016

INCLUDE ~AC_QUEST/tpa/wolves_and_men.tpa~


BEGIN @5000010 /* ~Quest uses house in Beregost.~ */
LABEL AscalonsQuestpack-WolvesMen_Beregost
SUBCOMPONENT @5000009 /* ~Von Wölfen und Männern~ */
REQUIRE_PREDICATE GAME_IS ~bgt bgee tutu_totsc eet~ @5000004
FORBID_COMPONENT ~EET_end.tp2~ ~0~ @5000016
FORBID_COMPONENT ~EET_end.tp2~ ~1~ @5000016

INCLUDE ~AC_QUEST/tpa/wolves_and_men.tpa~

EXTEND_TOP ~BALDUR.bcs~ ~AC_QUEST/BAF/ACBALDU3.BAF~

LAF extend_area_script STR_VAR area="%Beregost_House09%" bottom="AC_QUEST/BAF/AR6754.baf"   END

////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////
////////////																			////////////
////////////							7.	Der große Karlini							////////////
////////////																			////////////
////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////

BEGIN @5000011
LABEL AscalonsQuestpack-GreatKarlini
REQUIRE_PREDICATE GAME_IS ~bgt bgee tutu_totsc eet~ @5000004
FORBID_COMPONENT ~EET_end.tp2~ ~0~ @5000016
FORBID_COMPONENT ~EET_end.tp2~ ~1~ @5000016

ADD_JOURNAL TITLE (@2000009) @2000001 @2000002 @2000003 @2000004 @2000005 @2000006 @2000007 @2000008 USING ~AC_QUEST/tra/autotra/%LANGUAGE%/setup-AC_QUEST.tra~

COPY ~AC_QUEST/ITM/ACQ17003.ITM~ ~override~
SAY NAME1 #215
SAY NAME2 @47
SAY UNIDENTIFIED_DESC #6819
PATCH_IF GAME_IS ~tutu_totsc bgt~ BEGIN
SAY DESC @48
END
PATCH_IF GAME_IS ~bgee eet~ BEGIN
SAY DESC @100048
END


COMPILE ~AC_QUEST/D/ACQ17000.D~
EVAL
COMPILE EVALUATE_BUFFER ~AC_QUEST/BAF/ACQ17001.BAF~
COMPILE EVALUATE_BUFFER ~AC_QUEST/BAF/ACQ17002.BAF~
COMPILE EVALUATE_BUFFER ~AC_QUEST/BAF/ACQCUT03.BAF~
/*
EXTEND_BOTTOM ~%UlcasterRuins_BCS%.bcs~ ~AC_QUEST/BAF/AR9798.baf~
EVAL
EXTEND_TOP ~%LionsWay_BCS%.bcs~ ~AC_QUEST/BAF/AR6600.baf~
EVAL
*/
LAF extend_area_script STR_VAR area="%UlcasterRuins%" bottom="AC_QUEST/BAF/AR9798.baf"   END
LAF extend_area_script STR_VAR area="%LionsWay%" top="AC_QUEST/BAF/AR6600.baf"   END


COPY ~AC_QUEST/ITM/BOOK90.ITM~ ~OVERRIDE/ACQBOOK2.ITM~ 
SAY NAME1 @49
SAY NAME2 @49
SAY DESC @50

COPY ~AC_QUEST/cre/ACQ17001.CRE~ ~override~
SAY NAME1 @51
SAY NAME2 @52
SAY INITIAL_MEETING @80032
SAY DAMAGE @80033
SAY DYING @80034
SAY SELECT_COMMON1 @80032

COPY ~AC_QUEST/cre/ACQ17002.CRE~ ~override~
SAY NAME1 @53
SAY NAME2 @53
SAY INITIAL_MEETING @80032
SAY DAMAGE @80033
SAY DYING @80034
SAY SELECT_COMMON1 @80032

COPY ~AC_QUEST/cre/ACQ17003.CRE~ ~override~
SAY NAME1 @80035
SAY NAME2 @80035

COPY_EXISTING ~%FriendlyArmInn_L1%.are~ ~OVERRIDE~
LPF fj_are_structure
	INT_VAR
	fj_loc_x          = 917
	fj_loc_y          = 682
	fj_dest_x         = 917
	fj_dest_y         = 682
	fj_orientation    = 4
	STR_VAR
	fj_structure_type = actor
	fj_name           = Ygnatz
	fj_cre_resref     = ACQ17001
END

COPY_EXISTING ~%UlcasterRuins%.are~ ~OVERRIDE~
LPF fj_are_structure
INT_VAR
	fj_con_itm_idx    = 0
	fj_flags          = 1
STR_VAR
	fj_name           = ACQBOOK2
	fj_structure_type = itm
END

////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////
////////////																			////////////
////////////							8.	Die Kreischlingszucht						////////////
////////////																			////////////
////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////

BEGIN @5000012
LABEL AscalonsQuestpack-Gibberlings
REQUIRE_PREDICATE GAME_IS ~bgt bgee tutu_totsc eet~ @5000004
FORBID_COMPONENT ~EET_end.tp2~ ~0~ @5000016
FORBID_COMPONENT ~EET_end.tp2~ ~1~ @5000016

ADD_JOURNAL TITLE (@3000006) @3000001 @3000002 @3000003 @3000004 @3000005 USING ~AC_QUEST/tra/autotra/%LANGUAGE%/setup-AC_QUEST.tra~

COMPILE ~AC_QUEST/BAF/ACQ18002.BAF~
COMPILE ~AC_QUEST/D/ACQ18000.D~

ACTION_IF GAME_IS ~tutu_totsc~ THEN BEGIN
COPY_EXISTING ~%CentralBaldursGate_House1_L1%.are~ ~override~
  READ_ASCII 0x94 area_script
  PATCH_IF (("%area_script%" STRING_COMPARE_CASE "None")=0) BEGIN
  	WRITE_EVALUATED_ASCII 0x94 ~%CentralBaldursGate_House1_L1_BCS%~ (8)
  END
BUT_ONLY
END
/*
EXTEND_TOP ~%CentralBaldursGate_House1_L1_BCS%.bcs~ ~AC_QUEST/baf/AR7711.baf~
EXTEND_TOP ~%WSewers_BCS%.bcs~ ~AC_QUEST/baf/AR7324.baf~
EVAL
EXTEND_TOP ~%CentralSewers_BCS%.bcs~ ~AC_QUEST/baf/AR7325.baf~
EVAL
*/
LAF extend_area_script STR_VAR area="%CentralBaldursGate_House1_L1%" bottom="AC_QUEST/baf/AR7711.baf"   END
LAF extend_area_script STR_VAR area="%WSewers%" bottom="AC_QUEST/BAF/AR7324.baf"   END
LAF extend_area_script STR_VAR area="%CentralSewers%" bottom="AC_QUEST/baf/AR7325.baf"   END

ACTION_IF GAME_IS ~tutu_totsc~ THEN BEGIN
COPY_EXISTING ~%BaldursGateDocks_IronThrone_Cellar%.are~ ~override~
  READ_ASCII 0x94 area_script
  PATCH_IF (("%area_script%" STRING_COMPARE_CASE "None")=0) BEGIN
  	WRITE_EVALUATED_ASCII 0x94 ~%BaldursGateDocks_IronThrone_Cellar_BCS%~ (8)
  END
BUT_ONLY
END
/*
EXTEND_TOP ~%BaldursGateDocks_IronThrone_Cellar_BCS%.bcs~ ~AC_QUEST/baf/AR7611.baf~
EVAL
*/
LAF extend_area_script STR_VAR area="%BaldursGateDocks_IronThrone_Cellar%" bottom="AC_QUEST/baf/AR7611.baf"   END

COMPILE ~AC_QUEST/baf/AC7711.baf~

COPY ~AC_QUEST/ARE/AC7711.ARE~ ~override~
WRITE_EVALUATED_ASCII 0x8 ~%NWBaldursGate_House2_L1%~ #6
WRITE_EVALUATED_ASCII 0x374 ~%CentralBaldursGate%~ #6
WRITE_EVALUATED_ASCII 0x37c ~%CentralBaldursGate_House1_L1_EXIT%~ #8
COPY ~AC_QUEST/ARE/AC7712.ARE~ ~override~
WRITE_EVALUATED_ASCII 0x8 ~%NWBaldursGate_House2_L2%~ #6

COPY ~AC_QUEST/ITM/KEY09.ITM~ ~OVERRIDE/ACQKEY03.ITM~ 
SAY NAME1 @54
SAY NAME2 @54
SAY DESC @55

COPY ~AC_QUEST/ITM/ACQ18001.ITM~ ~override~
SAY NAME1 @56
SAY NAME2 @56
SAY UNIDENTIFIED_DESC @57
SAY DESC @57

COPY ~AC_QUEST/ITM/ACQ18002.ITM~ ~override~
SAY NAME1 #6664
SAY NAME2 @1000
SAY UNIDENTIFIED_DESC #6667
PATCH_IF GAME_IS ~tutu_totsc bgt~ BEGIN
SAY DESC @58
END
PATCH_IF GAME_IS ~bgee eet~ BEGIN
SAY DESC @100058
END


COPY ~AC_QUEST/ITM/MISC5D.ITM~ ~OVERRIDE/ACQ18003.ITM~ 
SAY NAME1 @59
SAY NAME2 @59
SAY DESC @60

COPY ~AC_QUEST/cre/ACQ18001.CRE~ ~override~
	 ~AC_QUEST/cre/ACQ18004.CRE~ ~override~
	 ~AC_QUEST/cre/ACQ18324.CRE~ ~override~
	 ~AC_QUEST/cre/ACQ18325.CRE~ ~override~
	 ~AC_QUEST/cre/ACQ18611.CRE~ ~override~
SAY NAME1 @80040
SAY NAME2 @80040

COPY ~AC_QUEST/cre/ACQ18002.CRE~ ~override~
SAY NAME1 @61
SAY NAME2 @62

COPY_EXISTING ~%CentralBaldursGate%.are~ ~OVERRIDE~
LPF fj_are_structure
    INT_VAR
    fj_loc_x          = 1354
    fj_loc_y          = 3081
    fj_dest_x         = 1354
    fj_dest_y         = 3081
    fj_orientation    = 1
    STR_VAR
    fj_structure_type = actor
    fj_name           = Geraldo
    fj_cre_resref     = ACQ18002
END
READ_LONG  0xa4  "doors_num"
READ_LONG  0xa8  "doors_offset"
    FOR( cnt=0; cnt<"%doors_num%"; cnt=cnt+1 ) BEGIN
        READ_ASCII ("%doors_offset%"+0xc8*cnt)       "door_name"
            PATCH_IF (("%door_name%" STRING_COMPARE_CASE "Door0711")=0) BEGIN
                SET offset = ("%doors_offset%"+0xc8*cnt)
                WRITE_LONG (offset+0x28) 0x00000442
                WRITE_ASCII (offset+0x78) ~ACQKEY03~ (8)
                WRITE_BYTE (offset+0x8c) 100
            END
END

////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////
////////////																			////////////
////////////							9.	Echte Fans									////////////
////////////																			////////////
////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////

BEGIN @5000013
LABEL AscalonsQuestpack-Fans
REQUIRE_PREDICATE GAME_IS ~bgt bgee tutu_totsc eet~ @5000004
FORBID_COMPONENT ~EET_end.tp2~ ~0~ @5000016
FORBID_COMPONENT ~EET_end.tp2~ ~1~ @5000016

COMPILE ~AC_QUEST/D/ACQ19000.D~
EVAL

COPY ~AC_QUEST/cre/ACQ19001.cre~ ~override~
SAY NAME1 @63
SAY NAME2 @63

COPY ~AC_QUEST/cre/ACQ19002.cre~ ~override~
SAY NAME1 @64
SAY NAME2 @64

COPY ~AC_QUEST/cre/ACQ19003.cre~ ~override~
SAY NAME1 @65
SAY NAME2 @65

COPY_EXISTING ~%NWBaldursGate%.are~ ~OVERRIDE~
LPF fj_are_structure
    INT_VAR
    fj_loc_x          = 2834
    fj_loc_y          = 2768
    fj_dest_x         = 2834
    fj_dest_y         = 2768
    fj_orientation    = 0
    STR_VAR
    fj_structure_type = actor
    fj_name           = Mandy
    fj_cre_resref     = ACQ19001
  END

LPF fj_are_structure
    INT_VAR
    fj_loc_x          = 2792
    fj_loc_y          = 2795
    fj_dest_x         = 2792
    fj_dest_y         = 2795
    fj_orientation    = 10
    STR_VAR
    fj_structure_type = actor
    fj_name           = Ashley
    fj_cre_resref     = ACQ19002
  END
  
LPF fj_are_structure
    INT_VAR
    fj_loc_x          = 2841
    fj_loc_y          = 2813
    fj_dest_x         = 2841
    fj_dest_y         = 2813
    fj_orientation    = 7
    STR_VAR
    fj_structure_type = actor
    fj_name           = Cindy
    fj_cre_resref     = ACQ19003
  END


////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////
////////////																			////////////
////////////							10.	Die Schlangen Abbathors Teil I				////////////
////////////																			////////////
////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////

BEGIN @5000017 /* ~Original version~ */
LABEL AscalonsQuestpack-SerpentsAbbathor
SUBCOMPONENT @5000014 /* ~The Serpents of Abbathor~ */
REQUIRE_PREDICATE GAME_IS ~bgt bgee tutu_totsc eet~ @5000004
FORBID_COMPONENT ~EET_end.tp2~ ~0~ @5000016
FORBID_COMPONENT ~EET_end.tp2~ ~1~ @5000016

INCLUDE ~AC_QUEST/tpa/serpents_of_abbathor.tpa~


BEGIN @5000018 /* ~Tweaked version~ */
LABEL AscalonsQuestpack-SerpentsAbbathor_Tweak
SUBCOMPONENT @5000014 /* ~The Serpents of Abbathor~ */
REQUIRE_PREDICATE GAME_IS ~bgt bgee tutu_totsc eet~ @5000004
FORBID_COMPONENT ~EET_end.tp2~ ~0~ @5000016
FORBID_COMPONENT ~EET_end.tp2~ ~1~ @5000016

LOAD_TRA ~AC_QUEST/tra/autotra/%s/serpents_of_abbathor_tweaked.tra~

INCLUDE ~AC_QUEST/tpa/serpents_of_abbathor.tpa~

//Wostok
ADD_JOURNAL TITLE (@3000052) @3000050 @3000051 USING ~AC_QUEST/tra/autotra/%LANGUAGE%/setup-AC_QUEST.tra~

//What to do with the Gundmagtor?
ADD_JOURNAL TITLE (@3000054) @3000055 @3000056 @3000057 @3000058 @3000059 @3000060 @3000061 USING ~AC_QUEST/tra/autotra/%LANGUAGE%/setup-AC_QUEST.tra~


COPY_EXISTING ~ACQ10111.cre~ ~override~
  REMOVE_CRE_ITEM ~ACQLET04~

/* Wostok can be picked up */
EXTEND_BOTTOM ~AC4400.bcs~ ~AC_QUEST/baf/AC4400_tweak.baf~ EVALUATE_BUFFER

COPY ~AC_QUEST/ITM/acq10127.itm~ ~OVERRIDE~ 
SAY NAME1 @1001 /* Wostoks Leiche */
SAY NAME2 @1001 /* Wostoks Leiche */
SAY DESC @1002 /* Dies ist die Leiche von Wostok, dem Zwergenkrieger. Der Priester im Tempel des Helm in Baldur's Gate wird sie Euch sicher abnehmen. */

ACTION_IF GAME_IS ~bgt~ THEN BEGIN
  COMPILE EVALUATE_BUFFER ~AC_QUEST/d/ACQ10100_tweak.D~
	~AC_QUEST/d/ACQ10100_tweak_bgt.d~
	USING ~AC_QUEST/tra/autotra/%LANGUAGE%/ACQ10100_tweak.tra~
END ELSE BEGIN
  COMPILE EVALUATE_BUFFER ~AC_QUEST/d/ACQ10100_tweak.D~ 
END

COMPILE EVALUATE_BUFFER ~AC_QUEST/baf/ACQSZENT.baf~

/* Zhentarim Emissary */
COPY_EXISTING ~%tutu_var%wheber.CRE~ ~override/ACQSZENT.CRE~
SAY NAME1 @1007 
SAY NAME2 @1007
WRITE_EVALUATED_ASCII 0x248 ~ACQSZENT~ #8   // Override script
WRITE_EVALUATED_ASCII 0x250 ~~ #8   // Class script
WRITE_EVALUATED_ASCII 0x258 ~~ #8   // Race script
WRITE_EVALUATED_ASCII 0x260 ~~ #8   // General script
WRITE_EVALUATED_ASCII 0x268 ~~ #8   // Default script
WRITE_EVALUATED_ASCII 0x280 ~ACQSZENT~ #8   // Script name
WRITE_EVALUATED_ASCII 0x2cc ~ACQSZENT~ #8   // Dialogue
ADD_CRE_ITEM ~MINHP1~ #0 #0 #0 ~unstealable&undroppable~ ~belt~

/* Place Zhentarim Emissary */
EXTEND_TOP ~BALDUR.bcs~ ~AC_QUEST/BAF/ACBALDU4.BAF~ EVALUATE_BUFFER
LAF extend_area_script STR_VAR area="%NEBaldursGate_BlushingMermaid_L1%" top="AC_QUEST/baf/AR7215.baf"   END

/* BG2 follow up: good path. for BGT and EET only */
/* use "unfinished business" :-) Idea from Lumorus! */
ACTION_IF GAME_IS ~bgt eet~ THEN BEGIN

//A Mysterious Offer
ADD_JOURNAL TITLE (@90010) @20023 @3000081 USING ~AC_QUEST/tra/autotra/%LANGUAGE%/setup-AC_QUEST.tra~

COMPILE EVALUATE_BUFFER ~AC_QUEST/d/ACQ21000.d~
COMPILE EVALUATE_BUFFER ~AC_QUEST/d/ACQ10100_tweak_bg2.D~ USING ~AC_QUEST/tra/autotra/%LANGUAGE%/ACQ10100_tweak.tra~

COMPILE EVALUATE_BUFFER ~AC_QUEST/baf/ACQ21001.baf~
COMPILE EVALUATE_BUFFER ~AC_QUEST/baf/ACQCUT04.baf~

/* Wostok's Hammer */
COPY ~AC_QUEST/ITM/acq10128.ITM~ ~override~
SAY NAME1 @1004 /* Wostok's Hammer +3 */
SAY NAME2 @1004
SAY DESC @1005

/* Wostok's Hammer improved by Cromwell */
COPY ~AC_QUEST/ITM/acq1128a.ITM~ ~override~
SAY NAME1 @1004 /* Wostok's Hammer +3 */
SAY NAME2 @1004
SAY DESC @1008

/* Wostok's Hammer improved by Cespenar: if hammer was improved by Cromwell */
COPY ~AC_QUEST/ITM/acq1128b.ITM~ ~override~
SAY NAME1 @1010 /* Wostok's Hammer +4 */
SAY NAME2 @1010
SAY DESC @1009

/* Wostok's Hammer improved by Cespenar: if hammer was not improved by Cromwell */
COPY ~AC_QUEST/ITM/acq1128c.ITM~ ~override~
SAY NAME1 @1011 /* Wostok's Hammer +5 */
SAY NAME2 @1011
SAY DESC @1012

/* Baldar Goldbart: Messenger dwarf */
COPY ~AC_QUEST/cre/ACQ10114.CRE~ ~override/ACQ21001.cre~
SAY NAME1 @1003 /* Baldar Goldbart */ 
SAY NAME2 @1003 /* Baldar Goldbart */
WRITE_EVALUATED_ASCII 0x248 ~ACQ21001~ #8   // Override script
WRITE_EVALUATED_ASCII 0x280 ~ACQ21001~ #8   // Script name
WRITE_EVALUATED_ASCII 0x2cc ~ACQ21001~ #8   // Dialogue

/* Gorgar Blaurock */
COPY ~AC_QUEST/cre/ACQ10113.CRE~ ~override/ACQ21002.cre~
SAY NAME1 @1006 /* Gorgar Blaurock */ 
SAY NAME2 @1006 /* Gorgar Blaurock */
WRITE_EVALUATED_ASCII 0x248 ~ACQ21002~ #8   // Override script
WRITE_EVALUATED_ASCII 0x280 ~ACQ21002~ #8   // Script name
WRITE_EVALUATED_ASCII 0x2cc ~ACQ21002~ #8   // Dialogue
ADD_CRE_ITEM ~acq10128~ #0 #0 #0 ~unstealable~ ~inv5~

EXTEND_BOTTOM ~AR0334.bcs~ ~AC_QUEST/BAF/AR0334.BAF~ EVALUATE_BUFFER
EXTEND_TOP ~AR0400.bcs~ ~AC_QUEST/BAF/AR0400.BAF~ EVALUATE_BUFFER
EXTEND_TOP ~AR0406.bcs~ ~AC_QUEST/BAF/AR0406.BAF~ EVALUATE_BUFFER
EXTEND_TOP ~BALDUR.bcs~ ~AC_QUEST/BAF/ACBALDU2.BAF~ EVALUATE_BUFFER
EXTEND_BOTTOM ~BOTSMITH.bcs~ ~AC_QUEST/BAF/ACQBOTSMITH.baf~ EVALUATE_BUFFER

END //BGT,EET


////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////
////////////																			////////////
////////////									BEGIN PART 2							////////////
////////////	

//ADD_JOURNAL TITLE (@90010) @20023																		////////////
////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////
         
// BEGIN ~Ascalons Questpack: Inhalte fuer BG 2~

////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////
////////////																			////////////
////////////						2. Ein Halbling auf Abwegen							////////////
////////////																			////////////
////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////
//ACTION_IF GAME_IS ~tob bg2ee eet~ THEN BEGIN

BEGIN @5000015
LABEL AscalonsQuestpack-HalflingEyeless
REQUIRE_PREDICATE GAME_IS ~bgt tob bg2ee eet~ @5000004
FORBID_COMPONENT ~EET_end.tp2~ ~0~ @5000016
FORBID_COMPONENT ~EET_end.tp2~ ~1~ @5000016

ADD_JOURNAL TITLE (@90009) @20001 @20002 @20003 @20004 @20005 @20006 USING ~AC_QUEST/tra/autotra/%LANGUAGE%/setup-AC_QUEST.tra~

COPY ~AC_QUEST/cre/ACQ22001.CRE~ ~override~
SAY NAME1 @123
SAY NAME2 @123

COPY ~AC_QUEST/cre/ACQ22002.CRE~ ~override~
SAY NAME1 @124
SAY NAME2 @124


COPY_EXISTING ~AR0900.are~ ~OVERRIDE~
LPF fj_are_structure
    INT_VAR
    fj_loc_x          = 3165
    fj_loc_y          = 1769
    fj_dest_x         = 3165
    fj_dest_y         = 1769
    fj_orientation    = 13
    STR_VAR
    fj_structure_type = actor
    fj_name           = Halfling
    fj_cre_resref     = ACQ22001
  END
  
COPY_EXISTING ~AR0202.are~ ~OVERRIDE~
LPF fj_are_structure
    INT_VAR
    fj_loc_x          = 1799
    fj_loc_y          = 1409
    fj_dest_x         = 1799
    fj_dest_y         = 1409
    fj_orientation    = 14
    STR_VAR
    fj_structure_type = actor
    fj_name           = Bunger
    fj_cre_resref     = ACQ22002
  END

COMPILE ~AC_QUEST/BAF/ACQ22001.BAF~
COMPILE ~AC_QUEST/BAF/ACQ22002.BAF~

EXTEND_TOP ~AR0202.bcs~ ~AC_QUEST/baf/AR0202.baf~
EVAL

COMPILE ~AC_QUEST/D/ACQ22000.D~

//END
